*************************************************************;
* DISCRIM.SAS -- Discriminant Analysis with Interpretation  *;
* Fisher Iris Data -- Discriminant Analysis Examples.       *;
* Part 1. Three population linear discriminant analysis.    *;
* Part 2. Three population quadratic discriminant analysis. *;
* Part 3. Three population canonical discriminant analysis. *;
* Part 4. Nonparametric nearest neighbor analysis.          *;
* Part 5. Kernel Smoothing Analysis							*;
* See Table 11.5 of Johnson and Wichern, 3rd Ed., p. 566.   *;
*;
*  SCRIPT PRIMARILY BY EDGAR 'BRADLEY' MOSER OF LOUSIANA STATE *;
*************************************************************;
Title1 "Discriminant Analysis of the Fisher Iris Data";

GOptions FText=SwissX  CText=Black  HText=1.0
         FTitle=SwissX CTitle=Black HTitle=1.0
         NoPrompt;

Options NoDate;

Proc Format;
   Value Specname
      1='SETOSA    '
      2='VERSICOLOR'
      3='VIRGINICA '
      Other='TIE';
Run;

Data Iris;
   Input sepallen sepalwid petallen petalwid species @@;
   y1=log(sepallen/sepalwid);
   y2=log(petallen/petalwid);
   Format species specname.;
   Label sepallen='Sepal Length in mm.'
         sepalwid='Sepal Width in mm.'
         petallen='Petal Length in mm.'
         petalwid='Petal Width in mm.'
         y1="Log Sepal Shape"
         y2="Log Petal Shape";
Datalines;
50 33 14 02 1 64 28 56 22 3 65 28 46 15 2 67 31 56 24 3
63 28 51 15 3 46 34 14 03 1 69 31 51 23 3 62 22 45 15 2
59 32 48 18 2 46 36 10 02 1 61 30 46 14 2 60 27 51 16 2
65 30 52 20 3 56 25 39 11 2 65 30 55 18 3 58 27 51 19 3
68 32 59 23 3 51 33 17 05 1 57 28 45 13 2 62 34 54 23 3
77 38 67 22 3 63 33 47 16 2 67 33 57 25 3 76 30 66 21 3
49 25 45 17 3 55 35 13 02 1 67 30 52 23 3 70 32 47 14 2
64 32 45 15 2 61 28 40 13 2 48 31 16 02 1 59 30 51 18 3
55 24 38 11 2 63 25 50 19 3 64 32 53 23 3 52 34 14 02 1
49 36 14 01 1 54 30 45 15 2 79 38 64 20 3 44 32 13 02 1
67 33 57 21 3 50 35 16 06 1 58 26 40 12 2 44 30 13 02 1
77 28 67 20 3 63 27 49 18 3 47 32 16 02 1 55 26 44 12 2
50 23 33 10 2 72 32 60 18 3 48 30 14 03 1 51 38 16 02 1
61 30 49 18 3 48 34 19 02 1 50 30 16 02 1 50 32 12 02 1
61 26 56 14 3 64 28 56 21 3 43 30 11 01 1 58 40 12 02 1
51 38 19 04 1 67 31 44 14 2 62 28 48 18 3 49 30 14 02 1
51 35 14 02 1 56 30 45 15 2 58 27 41 10 2 50 34 16 04 1
46 32 14 02 1 60 29 45 15 2 57 26 35 10 2 57 44 15 04 1
50 36 14 02 1 77 30 61 23 3 63 34 56 24 3 58 27 51 19 3
57 29 42 13 2 72 30 58 16 3 54 34 15 04 1 52 41 15 01 1
71 30 59 21 3 64 31 55 18 3 60 30 48 18 3 63 29 56 18 3
49 24 33 10 2 56 27 42 13 2 57 30 42 12 2 55 42 14 02 1
49 31 15 02 1 77 26 69 23 3 60 22 50 15 3 54 39 17 04 1
66 29 46 13 2 52 27 39 14 2 60 34 45 16 2 50 34 15 02 1
44 29 14 02 1 50 20 35 10 2 55 24 37 10 2 58 27 39 12 2
47 32 13 02 1 46 31 15 02 1 69 32 57 23 3 62 29 43 13 2
74 28 61 19 3 59 30 42 15 2 51 34 15 02 1 50 35 13 03 1
56 28 49 20 3 60 22 40 10 2 73 29 63 18 3 67 25 58 18 3
49 31 15 01 1 67 31 47 15 2 63 23 44 13 2 54 37 15 02 1
56 30 41 13 2 63 25 49 15 2 61 28 47 12 2 64 29 43 13 2
51 25 30 11 2 57 28 41 13 2 65 30 58 22 3 69 31 54 21 3
54 39 13 04 1 51 35 14 03 1 72 36 61 25 3 65 32 51 20 3
61 29 47 14 2 56 29 36 13 2 69 31 49 15 2 64 27 53 19 3
68 30 55 21 3 55 25 40 13 2 48 34 16 02 1 48 30 14 01 1
45 23 13 03 1 57 25 50 20 3 57 38 17 03 1 51 38 15 03 1
55 23 40 13 2 66 30 44 14 2 68 28 48 14 2 54 34 17 02 1
51 37 15 04 1 52 35 15 02 1 58 28 51 24 3 67 30 50 17 2
63 33 60 25 3 53 37 15 02 1
;

Proc Sort Data=Iris;
 By Species;
Run;

*Proc Print Data=Iris Split=" ";
*Run;

Title3 'Plot of Original Shape Measurements';
Proc GPlot Data=Iris;
 Plot y1*y2=Species;
 Symbol1 V=Dot    H=0.7 I=None C=Red;
 Symbol2 V=Star   H=0.7 I=None C=Green;
 Symbol3 V=Square H=0.7 I=None C=Blue;
Run; Quit;

*************************************************************;
* Construct some artificial Y1 and Y2 data to be classified.*;
* These observations can be used to plot and explore the    *;
* classification regions and to construct a classification  *;
* chart for classifying future observations. THIS IS THE GRID*;
*************************************************************;
 /* find the min and max values of y1 and y2 in the data set
    so we'll have an idea as to the potential range of y1 and y2
    values */
Proc Means Data=Iris NoPrint;
 Var Y1 Y2;
 Output Out=Stats Min=MinY1 MinY2 Max=MaxY1 MaxY2;
Run;

 /* Now generate the test values. These data will be used
    and reported using the testout=, testdata=, and testoutd=
    options on the Proc Discrim statement. */
Data PlotData;
 If _N_=1 Then Set Stats;
 Y1Inc=(MaxY1-MinY1)/50;
 Y2Inc=(MaxY2-MinY2)/50;
 Do Y1 = (MinY1-Y1Inc) To (MaxY1+Y1Inc) By Y1Inc;
   Do Y2 = (MinY2-Y2Inc) To (MaxY2+Y2Inc) By Y2Inc;
     Output;
     Keep Y1 Y2;
   End;
 End;
 Stop;
Run;

*PLOT OF THE GRID POINTS;
Title2 "Grid Data Set For Predictions";
Proc GPlot Data=PlotData;
 Plot Y1*Y2=1 / VAxis=Axis1 HAxis=Axis2;
 Axis1 Order=(0.2 To 1.1 By 0.1);
 Axis2 Order=(0.7 To 2.7 By 0.5);
 Symbol1 V=Dot H=0.3 I=None C=Black;
Run; Quit;


%Macro Graphs;
%* Produce various plots of densities and probabilities *;
%* for the discriminant analysis.                       *;

Title3 'Plot of Estimated Densities';
Proc GContour Data=PlotD;
 Title4 "Iris setosa";
 Plot y1*y2=Setosa;
Run;
Proc G3D Data=PlotD;
 Plot y1*y2=Setosa / Rotate=0;
Run;

Proc GContour Data=PlotD;
 Title4 "Iris versicolor";
 Plot y1*y2=Versicolor;
Run;
Proc G3D Data=PlotD;
 Plot y1*y2=Versicolor / Rotate=0;
Run;

Proc GContour Data=PlotD;
 Title4 "Iris virginica";
 Plot y1*y2=Virginica;
Run;
Proc G3D Data=PlotD;
 Plot y1*y2=Virginica / Rotate=0;
Run;

Title3 'Plot of Posterior Probabilities';
Proc GContour Data=PlotP;
 Title4 "Iris setosa";
 Plot y1*y2=Setosa;
Run;
Proc G3D Data=PlotP;
 Plot y1*y2=Setosa / Rotate=0;
Run;

Proc GContour Data=PlotP;
 Title4 "Iris versicolor";
 Plot y1*y2=Versicolor;
Run;
Proc G3D Data=PlotP;
 Plot y1*y2=Versicolor / Rotate=0;
Run;

Proc GContour Data=PlotP;
 Title4 "Iris virginica";
 Plot y1*y2=Virginica;
Run;
Proc G3D Data=PlotP;
 Plot y1*y2=Virginica / Rotate=0;
Run;

Data PlotP;
 Set PlotP;
 If _Into_=. Then _Into_=99;
Run;

Title3 'Plot of Classification Results';
Proc GPlot Data=PlotP;
 Plot y1*y2=_Into_ / Legend=Legend1 VAxis=Axis1 HAxis=Axis2;
 Legend1 Label=("Classifed Into");
 Axis1 Order=(0.2 To 1.1 By 0.1);
 Axis2 Order=(0.7 To 2.7 By 0.5);
 Symbol1 V=Dot    H=0.7 I=None C=Red;
 Symbol2 V=Star   H=0.7 I=None C=Green;
 Symbol3 V=Square H=0.7 I=None C=Blue;
 Symbol4 V=Dot    H=0.7 I=None C=Yellow;
Run; Quit;
%Mend;

*************************************************************;
* Part 1. Linear Discriminant Analysis                      *;
*************************************************************;

Title2 'Linear Discriminant Function Analysis';
Proc Discrim Data=Iris
             Testdata=Plotdata TestOut=PlotP TestOutD=PlotD
             Method=Normal Pool=Yes Crossvalidate;
  Class Species;
  Var y1 y2;
 Priors Prop;
Run;

%Graphs;

*************************************************************;
* Part 2. Quadratic Discriminant Analysis.                  *;
*************************************************************;

Title2 'Quadratic Discriminant Function Analysis';
Proc Discrim Data=Iris
             TestData=PlotData TestOut=PlotP TestOutD=PlotD
             Method=Normal Pool=No Short Crossvalidate;
 Class Species;
 Var y1 y2;
 Priors Prop;
Run;

%Graphs;

*************************************************************;
* Part 3. Canonical Discriminant Analysis.                  *;
*************************************************************;

Title2 "Canonical Discriminant Function Analysis";
Proc CanDisc Data=Iris Out=OIris;
 Class Species;
 Var y1 y2;
Run;

/*
 * Run the following macro to show or verify
 * what the canonical structure and standardized
 * coefficients in the canonical discriminant
 * analysis are.
 */
%Macro ShowStats;
Title3 "Show What Canonical Structure Is";
Proc Corr Data=OIris;
 Var Can1 Can2;
 With y1 y2;
Run;

Title3 "Show What Standardized Coefficients Are";
Proc Reg Data=OIris;
 Model Can1 Can2 = y1 y2;
Run; Quit;
%Mend ShowStats;
%*ShowStats; /* <-- remove the asterisk following the % to activate */

Title3 "Plot of Observations In Space of Canonical Variates";
Proc GPlot Data=OIris;
 Plot Can1*Can2=Species;
 Symbol1 V=Dot    H=0.7 I=None C=Black;
 Symbol2 V=Star   H=0.7 I=None C=Black;
 Symbol3 V=Square H=0.7 I=None C=Black;
Run;
Quit;

*************************************************************;
* Part 4. Nearest Neighbor Classification Analysis.         *;
*************************************************************;
/*
 * You might experiment with several different choices for K.
 */
Title2 "K-nearest Neighbor Classification K=15";
Proc Discrim Data=Iris
     Method=NPar K=15 Metric=Full
     Testdata=Plotdata TestOut=PlotP TestOutD=PlotD
     Crossvalidate;
 Class Species;
 Var Y1 Y2;
 Priors Prop;
Run;

%Graphs;

quit;

*************************************************************;
* Part 5. Kernel Smoothing Analysis.         *;
*************************************************************;
/*
 * You might experiment with several different choices for R and Kernel.
 */
Title2 "Kernel Smoothing Classification R=0.75, Kernel=Normal";
Proc Discrim Data=Iris
     Method=NPar R=0.75 kernel=normal Metric=Full
     Testdata=Plotdata TestOut=PlotP TestOutD=PlotD
     Crossvalidate;
 Class Species;
 Var Y1 Y2;
 Priors Prop;
Run;
quit;

%Graphs;

Title2 "Kernel Smoothing Classification R=0.1, Kernel=Normal";
Proc Discrim Data=Iris
     Method=NPar R=0.1 kernel=normal Metric=Full
     Testdata=Plotdata TestOut=PlotP TestOutD=PlotD
     Crossvalidate;
 Class Species;
 Var Y1 Y2;
 Priors Prop;
Run;


%Graphs;


*ODS RTF Close;
