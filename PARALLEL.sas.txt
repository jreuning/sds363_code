*THIS MACRO CALCULATES VALUES FOR DETERMINING NUMBER OF PRINCIPLE 
COMPONENTS TO RETAIN.  IT COMPUTES THE FIRST 10 CUT OFF VALUES FOR 
TWO PARALLAL METHODS (LONGMAN AND ALLEN) AND THE BROKEN STICK METHOD
(FRONTIER).  ;

OPTIONS  LINESIZE=132 PAGESIZE=54 NOFMTERR NODATE NONUMBER CENTER;
RUN;


DATA PARALLELBASE;
  INPUT C1-C8;
  CARDS ;
  0.0316 0.7611 -0.0979 -0.3138 0.9794 -.2059 .1226 0
  0.1162 0.8613 -0.1122 -0.9281 -0.3781 0.0461 0.0040 1.0578
  0.1835 0.9436 -0.1237 -1.4173 -0.3306 0.0424 .0003 1.0805
  0.2578 1.0636 -0.1388 -1.9976 -0.2795 0.0364 -.0003 1.0714
  0.3171 1.1370 -0.1494 -2.4200 -0.2670 0.0360 -.0024 1.08994
  0.3809 1.2213 -0.1619 -2.8644 -0.2632 0.0368 -.0040 1.1039
  0.4492 1.3111 -0.1751 -3.3392 -0.2580 0.0360 -.0039 1.1173
  0.5309 1.4265 -0.1925 -3.8950 -0.2544 0.0373 -.0064 1.1421
  0.5734 1.4818 -0.1986 -4.2420 -0.2111 0.0329 -.0079 1.1229
  0.6460 1.5802 -0.2134 -4.7384 -0.1964 0.0310 -.0083 1.1320
;
RUN;

*THIS MACRO REQUIRES TWO ARGUMENTS :
  N = TOTAL NUMBER OF NON-MISSING OBSERVATIONS
  P = NUMBER OF VARIABLES (I.E. THE DIMENSION OF THE DATASET;

%MACRO PARALLEL (N,P); 

DATA STICK; RETAIN COUNT 0;
  DO WHILE (COUNT<&P);
    COUNT=COUNT+1;
	OUTPUT;
  END;
RUN;
PROC SORT DATA=STICK; BY DESCENDING COUNT;

DATA STICK; SET STICK; RETAIN CUMRATIO 0;
  RATIO=1/COUNT;
  CUMRATIO=CUMRATIO+RATIO;
  KEEP COUNT CUMRATIO;
  RENAME COUNT=COUNTER;
RUN;

PROC SORT DATA=STICK; BY DESCENDING CUMRATIO;
DATA STICK; SET STICK; IF COUNTER<11;
  RENAME CUMRATIO=STICK;

DATA PARALLEL; SET PARALLELBASE;
  RETAIN COUNTER 0 LEIG0 0;
  COUNTER=COUNTER+1;
  LN=LOG(&N);
  LP=LOG(&P);
  LEIG = C1*LN +C2*LP +C3*(LN*LP)+C4; 
  LAM =EXP(LEIG);
  LEIG1 = C5+C6*LOG(&N-1)+C7*LOG((&P-COUNTER-1)*(&P-COUNTER+2)/2)+C8*LEIG0;
  LAMB =EXP(LEIG1);
  LEIG0=LEIG1;
  N=&N;
  P=&P;
  KEEP LAM LAMB COUNTER;
  RENAME LAM=LONGMAN LAMB=ALLEN;
RUN;

DATA PARALLEL; MERGE STICK PARALLEL; BY COUNTER; RUN;
%MEND;

*TO RUN THIS MACRO, UNCOMMENT THE STATEMENT BELOW;
*REPLACE 139 WITH THE NUMBER OF NON-MISSING OBSERVATIONS;
*REPLACE 13 WITH THE NUMBER OF VARIABLES;
*%PARALLEL(139,13)